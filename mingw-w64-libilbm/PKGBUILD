# Maintainer: Alx Sa <cmyk.student@gmail.com>

_realname=libilbm
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1
_realver=0.1
pkgrel=1
pkgdesc="Parser library for the ILBM: IFF Interleaved BitMap format (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/svanderburg/libilbm"
license=('MIT')
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-libiff"
             'help2man'
             'git')
_appcommit='170c269bce23423f1163545ed9344482b2cf4487'
source=("${_realname}"::"git+https://github.com/svanderburg/libilbm.git#commit=$_appcommit")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_realname}"
  echo "$(cat version)"
}

prepare() {
  cd "${srcdir}"/${_realname}

  ./bootstrap
}

build() {
  cd "${srcdir}"/${_realname}

  ./configure \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --target="${MINGW_CHOST}" \
    --enable-static \
    --enable-shared

  make -j 1
}

package() {
  cd "${srcdir}"/${_realname}
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_realname}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
